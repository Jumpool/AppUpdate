apply plugin: 'maven-publish'
apply plugin: 'signing'

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}
Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                groupId "com.github.jumpool"
                artifactId project.ARTIFACT_ID
                version rootProject.ext.publishVersion
                artifact generateSourcesJar
                pom {
                    name = 'AppUpdate'
                    description = 'Android App update library. Android版本更新库，简单、轻量、可随意定制'
                    url = 'https://github.com/jumpool/AppUpdate'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'jumpool'
                            name = 'jumpool'
                            email = 'alvin.aj.jiao@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/jumpool/AppUpdate.git'
                        developerConnection = 'https://github.com/jumpool/AppUpdate.git'
                        url = 'https://github.com/jumpool/AppUpdate'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username localProperties.getProperty('sonatype.name')
                    password localProperties.getProperty('sonatype.password')
                }
            }
        }
    }
}
signing {
    sign publishing.publications
}
